plugins {
	id 'java'
	id 'war'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
}

repositories {
	mavenCentral()
}

// 하위 모든 프로젝트 공통 세팅
subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group 'org.example'
	version '1.0-SNAPSHOT'
	sourceCompatibility = 17

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	// 하위 모듈에서 공통으로 사용하는 세팅 추가
	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"  // configuration 자동완성
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'com.google.code.gson:gson:2.8.9'

		testImplementation( 'org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'  // junit4 사용 X
		}
	}

	test {
		useJUnitPlatform()
	}
}

project(':file-drive-core') {
	bootJar { enabled = false }
	jar { enabled = true }

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'mysql:mysql-connector-java:8.0.23'
		implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.2.1'
	}
}

project(':file-drive-api') {
	bootJar { enabled = true }
	jar { enabled = false }

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'  // Swagger
		implementation 'com.google.firebase:firebase-admin:9.2.0'  // firebase

		// security
		implementation 'org.springframework.boot:spring-boot-starter-security'
		implementation 'org.springframework.security:spring-security-test'

		// jwt
		implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
		implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
		implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

		// model mapper
		implementation 'org.modelmapper:modelmapper:2.4.4'

		implementation project(':file-drive-core')
	}
}
