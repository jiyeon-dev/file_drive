plugins {
	id 'java'
	id 'war'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
}

repositories {
	mavenCentral()
}

// 하위 모든 프로젝트 공통 세팅
subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group 'org.example'
	version '1.0-SNAPSHOT'
	sourceCompatibility = 17

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	// 하위 모듈에서 공통으로 사용하는 세팅 추가
	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"  // configuration 자동완성
		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
	}

	test {
		useJUnitPlatform()
	}
}

project(':file-driver-core') {
	bootJar { enabled = false }
	jar { enabled = true }

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'mysql:mysql-connector-java:8.0.23'
	}
}

project(':file-driver-api') {
	bootJar { enabled = true }
	jar { enabled = false }

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'  // Swagger
	}
}
